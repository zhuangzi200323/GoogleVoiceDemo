plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version "0.9.4"
}

ext {
    grpcVersion = '1.60.0'
    protocVersion = '3.25.1'
}

android {
    namespace 'com.sq.googlevoicedemo'
    compileSdk 33

    defaultConfig {
        applicationId "com.sq.googlevoicedemo"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }

            proto {//注意：不指定时，默认与java同级的目录(即${ProjectName}/app/src/main/java)
                srcDir 'src/main/proto'
                //include '**/*.proto'
            }
        }
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
    // Java V2
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/INDEX.LIST'
        //https://stackoverflow.com/questions/47298895/more-than-one-file-was-found-with-os-independent-path-protobuf-meta
        pickFirst 'field_mask.proto' //More than one file was found with OS independent path 'google/protobuf/field_mask.proto'.
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
//        javalite {
//            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
//        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                //Caused by: java.lang.VerifyError: Verifier rejected class com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder:
                // com.google.protobuf.SingleFieldBuilderV3 com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder
                // .getOutputAudioConfigMaskFieldBuilder() failed to verify: com.google.protobuf.SingleFieldBuilderV3
                // com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder.getOutputAudioConfigMaskFieldBuilder(): [0x12] register v1
                // has type Precise Reference: com.google.protobuf.FieldMask but expected Reference: com.google.protobuf.AbstractMessageVerifier
                // rejected class com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder: com.google.cloud.dialogflow.v2
                // .DetectIntentRequest$Builder com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder
                // .mergeOutputAudioConfigMask(com.google.protobuf.FieldMask) failed to verify: com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder
                // com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder.mergeOutputAudioConfigMask(com.google.protobuf.FieldMask):
                // [0x1E] register v2 has type Precise Reference: com.google.protobuf.FieldMask but expected Reference: com.google.protobuf.AbstractMessage
                //com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder
                // .setOutputAudioConfigMask(com.google.protobuf.FieldMask) failed to verify: com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder
                // com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder.setOutputAudioConfigMask(com.google.protobuf.FieldMask): [0xE] register
                // v2 has type Precise Reference: com.google.protobuf.FieldMask but expected Reference: com.google.protobuf.AbstractMessage
                // (declaration of 'com.google.cloud.dialogflow.v2.DetectIntentRequest$Builder' appears in /data/app/com.amy.googlevoice-1/base.apk)
                //at com.google.cloud.dialogflow.v2.DetectIntentRequest.toBuilder(DetectIntentRequest.java:717)
                //at com.google.cloud.dialogflow.v2.DetectIntentRequest.newBuilder(DetectIntentRequest.java:708)
                //at com.amy.googlevoice.RequestJavaV2Task.doInBackground(RequestJavaV2Task.java:31)
                //java { option 'lite' } //使用option: 'lite'时会报上方错误
                java {}
            }
            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                    //option 'enable_deprecated=false'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Java V2 0.110.0-alpha及之后的几个版本使用了protobuf-java:3.10.0(android O min-api>=26才可以用)，最新版本已经没有min-api限制
    implementation('com.google.cloud:google-cloud-dialogflow:4.38.0')
    // gRPC
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation("io.grpc:grpc-protobuf-lite:$grpcVersion"){
        exclude group: 'com.google.protobuf' //不导入protobuf，google-cloud-dialogflow会自动引入
    }
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "com.google.protobuf:protobuf-java:$protocVersion"
    //implementation "com.google.protobuf:protoc:$protocVersion"
    // OAuth2 for Google API
    implementation('com.google.auth:google-auth-library-oauth2-http:1.20.0') {
        exclude module: 'httpclient'
    }
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
}
